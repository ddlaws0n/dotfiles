# Main zsh configuration - ultra-minimal setup with antidote

# Suppress cd output during startup
export _SHELL_STARTING=1
export ZDOTDIR="${ZDOTDIR:-$HOME/.config/zsh}"

# Antidote plugin manager setup
zsh_plugins=${ZDOTDIR:-$HOME}/.zsh_plugins
[[ -f ${zsh_plugins}.txt ]] || touch ${zsh_plugins}.txt

source /opt/homebrew/share/antidote/antidote.zsh

# Generate static plugin file when bundle is newer
if [[ ! ${zsh_plugins}.zsh -nt ${zsh_plugins}.txt ]]; then
  echo "Generating antidote plugin cache..."
  antidote bundle <${zsh_plugins}.txt >${zsh_plugins}.zsh
fi

source ${zsh_plugins}.zsh

# Plugin configuration
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=240"
ZSH_AUTOSUGGEST_STRATEGY=(history completion)
ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE="20"
ZSH_AUTOSUGGEST_USE_ASYNC=1

# Fast-syntax-highlighting theme (using terminal colors for light theme compatibility)
if [[ -f "$ZDOTDIR/catppuccin-bright.ini" ]]; then
  source "$ZDOTDIR/catppuccin-bright.ini"
fi

# Completion configuration - daily cache for performance
autoload -Uz compinit
if [ "$(date +'%j')" != "$(stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)" ]; then
  compinit
else
  compinit -C
fi

# Completion styles
zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
zstyle ':completion:*:*:*:*:*' menu select
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;34=0=01'
zstyle ':completion:*:warnings' format ' %F{red}-- no matches found --%f'

# Bash completions support
autoload -U +X bashcompinit && bashcompinit

# Dynamic completions (only for tools requiring runtime generation)
# Note: Most tools are auto-loaded via FPATH from Homebrew/mise
# Only add here if tool requires dynamic generation AND isn't in FPATH

if command -v terraform &> /dev/null; then
  complete -o nospace -C terraform terraform  # Terraform uses bash-style completion
fi

if command -v kubectl &> /dev/null; then
  source <(kubectl completion zsh)  # Kubernetes requires dynamic generation
fi

if command -v op &> /dev/null; then
  eval "$(op completion zsh)"  # 1Password CLI requires dynamic generation
fi

if command -v docker &> /dev/null; then
  source <(docker completion zsh)  # Docker requires dynamic generation
fi

if command -v bun &> /dev/null && [[ -f "$BUN_INSTALL/_bun" ]]; then
  source "$BUN_INSTALL/_bun"  # Bun shell integration (more than just completion)
fi

# History configuration
HISTFILE=~/.zsh_history
HISTSIZE=10000
SAVEHIST=10000
setopt appendhistory sharehistory incappendhistory
setopt hist_ignore_all_dups hist_save_no_dups hist_ignore_space hist_reduce_blanks

# Performance optimizations
setopt no_beep                    # Silence terminal bells
export DISABLE_UNTRACKED_FILES_DIRTY=true  # Faster git status in large repos

# Enhanced file colors for light themes
export LS_COLORS="di=34:ln=35:so=32:pi=33:ex=31:bd=46;34:cd=43;34:su=41;30:sg=46;30:tw=42;30:ow=43;30"

# Smart history navigation keybinds
bindkey '^p' history-search-backward    # Ctrl+P for previous matching command
bindkey '^n' history-search-forward     # Ctrl+N for next matching command

# Load aliases
[[ -r "$ZDOTDIR/aliases.zsh" ]] && source "$ZDOTDIR/aliases.zsh"

# External integrations
[[ -f ~/.config/op/plugins.sh ]] && source ~/.config/op/plugins.sh

# Ghostty shell integration (enables enhanced terminal features)
if [[ -n $GHOSTTY_RESOURCES_DIR ]]; then
  source "$GHOSTTY_RESOURCES_DIR"/shell-integration/zsh/ghostty-integration
fi

# Initialize starship prompt
eval "$(starship init zsh)"

# Clear startup flag
unset _SHELL_STARTING